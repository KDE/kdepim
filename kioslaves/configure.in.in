KDE_CHECK_SSL

AC_ARG_WITH(sasl,
  [AC_HELP_STRING(--with-sasl,
    [enable support for authentication through cyrus-sasl @<:@default=check@:>@])],
  [], with_sasl=check)

sasl2_header="no"
SASL2_LIBS=""
if test "x$with_sasl" != xno; then
  KDE_CHECK_HEADERS(sasl/sasl.h, sasl2_header="yes")
  if test "$sasl2_header" = "yes" ; then
    KDE_CHECK_LIB(sasl2, sasl_client_init, SASL2_LIBS="-lsasl2")
  fi

  if test "x$SASL2_LIBS" != "x" ; then
    AC_DEFINE_UNQUOTED(HAVE_LIBSASL2, 1, [Define if you have cyrus-sasl2 libraries])
  fi

  if test "x$with_sasl" != xcheck && test -z "$SASL2_LIBS"; then
    AC_MSG_ERROR([--with-sasl was given, but test for cyrus-sasl failed])
  fi
fi

AC_SUBST(SASL2_LIBS)

AM_CONDITIONAL(compile_kio_sieve, test -n "$SASL2_LIBS")
