# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.

AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM 
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(kdegames, 2.0pre) dnl searches for some needed programs

dnl make $KDEDIR the default for the installation
AC_PREFIX_DEFAULT(${KDEDIR:-/usr/local/kde})

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS
KDE_PROG_LIBTOOL

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files
AM_KDE_WITH_NLS

dnl I need to check for libsocket before testing X
AC_CHECK_LIB(socket, socket, [LIBSOCKET="-lsocket -lnsl"]) dnl for Solaris
AC_SUBST(LIBSOCKET)
AC_PATH_KDE

AC_LANG_CPLUSPLUS
dnl Checks for libraries. 
AC_CHECK_LIB(compat, main, [LIBCOMPAT="-lcompat"]) dnl for FreeBSD
AC_SUBST(LIBCOMPAT)
AC_CHECK_LIB(crypt, main, [LIBCRYPT="-lcrypt"]) dnl for BSD
AC_SUBST(LIBCRYPT)
LIBKHTMLW="-lkhtmlw -ljscript" dnl this is somewhat historical, since jscript is ways needed
AC_SUBST(LIBKHTMLW)

dnl zlib check for ksokoban
AC_FIND_ZLIB

dnl Image readers/writers
KDE_CHECK_KIMGIO

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h sys/select.h)
AC_CHECK_FUNCS(usleep)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL

dnl Checks for library functions. MISCOBJS is for kdecore
AC_LANG_CPLUSPLUS
AC_CHECK_SETENV
AC_CHECK_RANDOM
dnl KDE_ADD_INCLUDES

AC_LANG_SAVE
kde_safe_cppflags=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $all_includes"
AC_LANG_CPLUSPLUS
AC_CHECK_HEADER(QwSpriteField.h, kde_has_qwspritefield_h=yes)
CPPFLAGS=$kde_safe_cppflags
AC_LANG_RESTORE

if test ! "x$kde_has_qwspritefield_h" = "xyes"; then
  AC_MSG_WARN(you don't have QwSpriteField.h somewhere. Please install
     QwSpriteField out of kdesupport.)
  DO_NOT_COMPILE="$DO_NOT_COMPILE kasteroids kspaceduel"
fi

dnl output files
topdir=`pwd`
AC_SUBST(topdir)

dnl Perform program name transformation
AC_ARG_PROGRAM

KDE_CREATE_SUBDIRSLIST

dnl this will be replaced by Makefile.am
AC_OUTPUT()
